<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="FB_MoveToPosition" Id="{d12a3672-81de-415e-a7e8-6515aef09e2b}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_MoveToPosition EXTENDS FB_StatusExecuteFB IMPLEMENTS ITF_ExtendsFB
VAR_IN_OUT
	fbMover								: MC_PlanarMover;
END_VAR
VAR_INPUT
	Execute								: BOOL;
	Position							: DUT_Position;
	Velocity							: LREAL;
	Acceleration						: LREAL;
	Deceleration						: LREAL;
	Jerk								: LREAL;
END_VAR
VAR_OUTPUT
	Done								: BOOL;
	Busy								: BOOL;
	CommandAborted						: BOOL;
	Error								: BOOL;
	ErrorID								: UDINT;
END_VAR
VAR
	uiState								: UINT;
	//Input 内部変数
	xycPosition							: PositionXYC;
	dutPosition							: DUT_Position;
	lrVelocity							: LREAL;
	lrAcceleration						: LREAL;
	lrDeceleration						: LREAL;
	lrJerk								: LREAL;
	
	//ローカル
	fbFB_Mover							: MC_PlanarFeedback;
	fbConstraint						: DynamicConstraint_PathXY;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[Prop_Execute := Execute;
METH_Main();

fbFB_Mover.Update();

Busy := bBusy;
Done := bDone;
CommandAborted := bCommandAborted;
Error := bError;
ErrorID := udiErrorID;]]></ST>
    </Implementation>
    <Method Name="METH_Busy" Id="{3b331b56-f438-4e2a-9693-0d2a8b7596f2}">
      <Declaration><![CDATA[{warning 'add method implementation '}
//実行動作
METHOD  METH_Busy : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF fbFB_Mover.Error THEN
	METH_Error();
	Prop_ErrorID := fbFB_Mover.ErrorId; //Moverがエラー
	RETURN;
END_IF
IF THIS^.fbMover.MCTOPLC.STD.State = MC_PLANAR_STATE.Error THEN
	METH_Error();
	Prop_ErrorID := 1; //Moverがエラー
	RETURN;
END_IF
IF fbFB_Mover.Aborted THEN
	METH_CommandAborted();
	RETURN;
END_IF

CASE THIS^.uiState OF
	0:
	xycPosition.SetValuesXYC(x:= dutPosition.x, y:= dutPosition.y, c:= 0);
	fbConstraint.SetValuesVADJ(V:= THIS^.lrVelocity, A:= THIS^.lrAcceleration, D:= THIS^.lrDeceleration, J:= THIS^.lrJerk);
	THIS^.fbMover.MoveToPosition(
		commandFeedback:= fbFB_Mover, 
		targetPosition:= xycPosition, 
		constraint:= fbConstraint, 
		options:= 0);
	THIS^.uiState := 10;
		
	10:
	IF fbFB_Mover.Done THEN
		THIS^.uiState := 16#FFFF;
	END_IF
	
	16#FFFF:
	METH_Done();
END_CASE]]></ST>
      </Implementation>
    </Method>
    <Method Name="METH_Enter" Id="{b4911010-408b-425e-b655-34d658467558}">
      <Declaration><![CDATA[METHOD  METH_Enter : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//Input 内部変数に移す
dutPosition := Position;
lrVelocity := Velocity;
lrAcceleration := Acceleration;
lrDeceleration := Deceleration;
lrJerk := Jerk;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="METH_Exit" Id="{9a86a1c4-cea8-47b2-b805-d29bff973eff}">
      <Declaration><![CDATA[METHOD  METH_Exit : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Prop_Done := FALSE;
Prop_Busy := FALSE;
Prop_CommandAborted := FALSE;
Prop_Error := FALSE;
Prop_ErrorID := 0;

// 内部変数をリフレッシュする
uiState := 0;
xycPosition.SetValuesXYC(0,0,0);
lrVelocity := 0;
lrAcceleration := 0;
lrDeceleration := 0;
lrJerk := 0;

]]></ST>
      </Implementation>
    </Method>
    <Method Name="METH_Main" Id="{a3313024-dfb1-40d3-8365-6ab04e1d1aed}">
      <Declaration><![CDATA[METHOD METH_Main : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//Doneに遷移
IF SUPER^.bDone AND SUPER^.bExecute THEN
	METH_Done();
	RETURN;
END_IF
//Errorに遷移
IF SUPER^.bError AND SUPER^.bExecute THEN
	METH_Error();
	RETURN;
END_IF
//CommandAbortedに遷移
IF SUPER^.bCommandAborted AND SUPER^.bExecute THEN
	METH_CommandAborted();
	RETURN;
END_IF
//FBをリフレッシュ
//状態がDone, Error, CommandAborted時に Execute FALSE で遷移
IF (SUPER^.bDone OR SUPER^.bError OR SUPER^.bCommandAborted) AND NOT SUPER^.bExecute THEN
	METH_Exit();
	RETURN;
END_IF

//実行開始
IF SUPER^.bExecute AND NOT SUPER^.bBusy THEN
	METH_Execute();
	METH_Enter();
END_IF
//実行中
IF SUPER^.bBusy THEN
	METH_Busy();
END_IF]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_MoveToPosition">
      <LineId Id="95" Count="0" />
      <LineId Id="9" Count="0" />
      <LineId Id="85" Count="0" />
      <LineId Id="42" Count="0" />
      <LineId Id="88" Count="4" />
      <LineId Id="86" Count="0" />
    </LineIds>
    <LineIds Name="FB_MoveToPosition.METH_Busy">
      <LineId Id="25" Count="0" />
      <LineId Id="27" Count="0" />
      <LineId Id="29" Count="0" />
      <LineId Id="46" Count="0" />
      <LineId Id="28" Count="0" />
      <LineId Id="39" Count="1" />
      <LineId Id="42" Count="0" />
      <LineId Id="47" Count="0" />
      <LineId Id="41" Count="0" />
      <LineId Id="43" Count="1" />
      <LineId Id="48" Count="0" />
      <LineId Id="45" Count="0" />
      <LineId Id="26" Count="0" />
      <LineId Id="4" Count="0" />
      <LineId Id="7" Count="0" />
      <LineId Id="35" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="9" Count="0" />
      <LineId Id="13" Count="3" />
      <LineId Id="21" Count="0" />
      <LineId Id="18" Count="2" />
      <LineId Id="22" Count="2" />
      <LineId Id="30" Count="1" />
      <LineId Id="8" Count="0" />
    </LineIds>
    <LineIds Name="FB_MoveToPosition.METH_Enter">
      <LineId Id="14" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="9" Count="4" />
    </LineIds>
    <LineIds Name="FB_MoveToPosition.METH_Exit">
      <LineId Id="22" Count="1" />
      <LineId Id="25" Count="0" />
      <LineId Id="24" Count="0" />
      <LineId Id="16" Count="1" />
      <LineId Id="5" Count="0" />
      <LineId Id="18" Count="0" />
      <LineId Id="10" Count="3" />
      <LineId Id="9" Count="0" />
      <LineId Id="15" Count="0" />
      <LineId Id="14" Count="0" />
    </LineIds>
    <LineIds Name="FB_MoveToPosition.METH_Main">
      <LineId Id="24" Count="0" />
      <LineId Id="14" Count="0" />
      <LineId Id="16" Count="0" />
      <LineId Id="18" Count="0" />
      <LineId Id="39" Count="0" />
      <LineId Id="20" Count="3" />
      <LineId Id="19" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="27" Count="3" />
      <LineId Id="40" Count="0" />
      <LineId Id="45" Count="0" />
      <LineId Id="41" Count="0" />
      <LineId Id="44" Count="0" />
      <LineId Id="42" Count="1" />
      <LineId Id="38" Count="0" />
      <LineId Id="15" Count="0" />
      <LineId Id="32" Count="1" />
      <LineId Id="36" Count="0" />
      <LineId Id="51" Count="0" />
      <LineId Id="35" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="12" Count="1" />
    </LineIds>
  </POU>
</TcPlcObject>