<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_Rotate_C" Id="{f60e97c7-9244-4416-aa65-a6d8a69ee8e7}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FINAL FB_Rotate_C EXTENDS FB_StatusExecuteFB
VAR_IN_OUT
	fbMover								: MC_PlanarMover;
END_VAR
VAR_INPUT
	Execute								: BOOL;
	Position							: ST_POSITION;
	Velocity							: LREAL;
	Acceleration						: LREAL;
	Deceleration						: LREAL;
	Jerk								: LREAL;
END_VAR
VAR_OUTPUT
	Done								: BOOL;
	Busy								: BOOL;
	CommandAborted						: BOOL;
	Error								: BOOL;
	ErrorID								: UDINT;
END_VAR
VAR
	uiState								: UINT;
	//Input 内部変数
	dutPosition							: ST_POSITION;
	lrVelocity							: LREAL;
	lrAcceleration						: LREAL;
	lrDeceleration						: LREAL;
	lrJerk								: LREAL;
	
	//ローカル
	fbFB_Mover							: MC_PlanarFeedback;
	dutTargetPosition					: ST_POSITION;
	fbConstraint						: DynamicConstraint_PathXY;
	fbMoveC								: FB_MoveC;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[Prop_Execute := Execute;
METH_Main();

fbFB_Mover.Update();

Busy := bBusy;
Done := bDone;
CommandAborted := bCommandAborted;
Error := bError;
ErrorID := udiErrorID;]]></ST>
    </Implementation>
    <Method Name="METH_Busy" Id="{60af7416-e5ab-40f2-b42e-1ad79d0f10d7}">
      <Declaration><![CDATA[//{error 'add method implementation or delete method to use base implementation'}
METHOD METH_Busy : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF fbFB_Mover.Error THEN
	METH_Error();
	Prop_ErrorID := fbFB_Mover.ErrorId; //Moverがエラー
	RETURN;
END_IF
IF THIS^.fbMover.MCTOPLC.STD.State = MC_PLANAR_STATE.Error THEN
	METH_Error();
	Prop_ErrorID := 1; //Moverがエラー
	RETURN;
END_IF
IF fbFB_Mover.Aborted THEN
	METH_CommandAborted();
	RETURN;
END_IF

CASE THIS^.uiState OF
	0:
	//+Cに回転
	dutTargetPosition.c := dutPosition.c;
	fbMoveC(
		fbMover:= THIS^.fbMover, 
		Execute:= TRUE, 
		Position:= dutTargetPosition, 
		Velocity:= THIS^.lrVelocity, 
		Acceleration:= THIS^.lrAcceleration, 
		Deceleration:= THIS^.lrDeceleration, 
		Jerk:= THIS^.lrJerk);
	IF fbMoveC.Done THEN
		fbMoveC(fbMover:= THIS^.fbMover, Execute:= FALSE);
		THIS^.uiState := 10;
	ELSIF fbMoveC.Error THEN
		METH_Error();
		Prop_ErrorID := 10; //Moverがエラー
	END_IF
	
	10:
	//-Cに回転
	dutTargetPosition.c := (-1)*dutPosition.c;
	fbMoveC(
		fbMover:= THIS^.fbMover, 
		Execute:= TRUE, 
		Position:= dutTargetPosition, 
		Velocity:= THIS^.lrVelocity, 
		Acceleration:= THIS^.lrAcceleration, 
		Deceleration:= THIS^.lrDeceleration, 
		Jerk:= THIS^.lrJerk);
	IF fbMoveC.Done THEN
		fbMoveC(fbMover:= THIS^.fbMover, Execute:= FALSE);
		THIS^.uiState := 20;
	ELSIF fbMoveC.Error THEN
		METH_Error();
		Prop_ErrorID := 11; //Moverがエラー
	END_IF
	
	20:
	//+Cに回転
	dutTargetPosition.c := 0;
	fbMoveC(
		fbMover:= THIS^.fbMover, 
		Execute:= TRUE, 
		Position:= dutTargetPosition, 
		Velocity:= THIS^.lrVelocity, 
		Acceleration:= THIS^.lrAcceleration, 
		Deceleration:= THIS^.lrDeceleration, 
		Jerk:= THIS^.lrJerk);
	IF fbMoveC.Done THEN
		fbMoveC(fbMover:= THIS^.fbMover, Execute:= FALSE);
		THIS^.uiState := 16#FFFF;
	ELSIF fbMoveC.Error THEN
		METH_Error();
		Prop_ErrorID := 12; //Moverがエラー
	END_IF
	
	16#FFFF:
	METH_Done();
END_CASE]]></ST>
      </Implementation>
    </Method>
    <Method Name="METH_Enter" Id="{fa9f6fb2-7d62-4698-8cbd-445d918d2f20}">
      <Declaration><![CDATA[//{error 'add method implementation or delete method to use base implementation'}
METHOD METH_Enter : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//Input 内部変数に移す
dutPosition := Position;
lrVelocity := Velocity;
lrAcceleration := Acceleration;
lrDeceleration := Deceleration;
lrJerk := Jerk;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="METH_Exit" Id="{67ec6446-4911-43da-a96f-ad11c1ad81da}">
      <Declaration><![CDATA[//{error 'add method implementation or delete method to use base implementation'}
METHOD METH_Exit : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Prop_Done := FALSE;
Prop_Busy := FALSE;
Prop_CommandAborted := FALSE;
Prop_Error := FALSE;
Prop_ErrorID := 0;

// 内部変数をリフレッシュする
uiState := 0;
lrVelocity := 0;
lrAcceleration := 0;
lrDeceleration := 0;
lrJerk := 0;]]></ST>
      </Implementation>
    </Method>
    <Method Name="METH_Main" Id="{27f9a07d-7bde-408d-9eb9-75cd1273a409}">
      <Declaration><![CDATA[//{error 'add method implementation or delete method to use base implementation'}
METHOD METH_Main : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//Doneに遷移
IF SUPER^.bDone AND SUPER^.bExecute THEN
	METH_Done();
	RETURN;
END_IF
//Errorに遷移
IF SUPER^.bError AND SUPER^.bExecute THEN
	METH_Error();
	RETURN;
END_IF
//CommandAbortedに遷移
IF SUPER^.bCommandAborted AND SUPER^.bExecute THEN
	METH_CommandAborted();
	RETURN;
END_IF
//FBをリフレッシュ
//状態がDone, Error, CommandAborted時に Execute FALSE で遷移
IF (SUPER^.bDone OR SUPER^.bError OR SUPER^.bCommandAborted) AND NOT SUPER^.bExecute THEN
	METH_Exit();
	RETURN;
END_IF

//実行開始
IF SUPER^.bExecute AND NOT SUPER^.bBusy THEN
	METH_Execute();
	METH_Enter();
END_IF
//実行中
IF SUPER^.bBusy THEN
	METH_Busy();
END_IF]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_Rotate_C">
      <LineId Id="42" Count="8" />
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_Rotate_C.METH_Busy">
      <LineId Id="5" Count="17" />
      <LineId Id="159" Count="0" />
      <LineId Id="23" Count="16" />
      <LineId Id="185" Count="15" />
      <LineId Id="55" Count="2" />
      <LineId Id="201" Count="15" />
      <LineId Id="73" Count="0" />
      <LineId Id="92" Count="2" />
      <LineId Id="4" Count="0" />
    </LineIds>
    <LineIds Name="FB_Rotate_C.METH_Enter">
      <LineId Id="5" Count="5" />
      <LineId Id="4" Count="0" />
    </LineIds>
    <LineIds Name="FB_Rotate_C.METH_Exit">
      <LineId Id="5" Count="7" />
      <LineId Id="14" Count="2" />
      <LineId Id="4" Count="0" />
    </LineIds>
    <LineIds Name="FB_Rotate_C.METH_Main">
      <LineId Id="5" Count="29" />
      <LineId Id="4" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>