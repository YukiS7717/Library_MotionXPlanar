<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_Tilt_B" Id="{2ed1768e-b4da-49d1-a789-a6fcd7f3308e}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FINAL FB_Tilt_B EXTENDS FB_StatusExecuteFB
VAR_IN_OUT
	fbMover								: MC_PlanarMover;
END_VAR
VAR_INPUT
	Execute								: BOOL;
	Position							: ST_POSITION;
	Velocity							: LREAL;
	Acceleration						: LREAL;
	Deceleration						: LREAL;
	Jerk								: LREAL;
END_VAR
VAR_OUTPUT
	Done								: BOOL;
	Busy								: BOOL;
	CommandAborted						: BOOL;
	Error								: BOOL;
	ErrorID								: UDINT;
END_VAR
VAR
	uiState								: UINT;
	//Input 内部変数
	dutPosition							: ST_POSITION;
	lrVelocity							: LREAL;
	lrAcceleration						: LREAL;
	lrDeceleration						: LREAL;
	lrJerk								: LREAL;
	
	//ローカル
	fbFB_Mover							: MC_PlanarFeedback;
	dutTargetPosition					: ST_POSITION;
	fbConstraint						: DynamicConstraint_PathXY;
	fbMoveB								: FB_MoveB;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[Prop_Execute := Execute;
METH_Main();

fbFB_Mover.Update();

Busy := bBusy;
Done := bDone;
CommandAborted := bCommandAborted;
Error := bError;
ErrorID := udiErrorID;]]></ST>
    </Implementation>
    <Method Name="METH_Busy" Id="{abebaa81-e96c-4a65-81b1-fa4ac19ac3f4}">
      <Declaration><![CDATA[//{error 'add method implementation or delete method to use base implementation'}
METHOD INTERNAL METH_Busy : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF fbFB_Mover.Error THEN
	METH_Error();
	Prop_ErrorID := fbFB_Mover.ErrorId; //Moverがエラー
	RETURN;
END_IF
IF THIS^.fbMover.MCTOPLC.STD.State = MC_PLANAR_STATE.Error THEN
	METH_Error();
	Prop_ErrorID := 1; //Moverがエラー
	RETURN;
END_IF
IF fbFB_Mover.Aborted THEN
	METH_CommandAborted();
	RETURN;
END_IF

CASE THIS^.uiState OF
	0:
	//+Aに傾ける
	dutTargetPosition.b := dutPosition.b;
	fbMoveB(
		fbMover:= THIS^.fbMover, 
		Execute:= TRUE, 
		Position:= dutTargetPosition, 
		Velocity:= THIS^.lrVelocity, 
		Acceleration:= THIS^.lrAcceleration, 
		Deceleration:= THIS^.lrDeceleration, 
		Jerk:= THIS^.lrJerk);
	IF fbMoveB.Done THEN
		fbMoveB(fbMover:= THIS^.fbMover, Execute:= FALSE);
		THIS^.uiState := 10;
	ELSIF fbMoveB.Error THEN
		METH_Error();
		Prop_ErrorID := 10; //Moverがエラー
	END_IF
	
	10:
	//-Bに傾ける
	dutTargetPosition.b := -1*dutPosition.b;
	fbMoveB(
		fbMover:= THIS^.fbMover, 
		Execute:= TRUE, 
		Position:= dutTargetPosition, 
		Velocity:= THIS^.lrVelocity, 
		Acceleration:= THIS^.lrAcceleration, 
		Deceleration:= THIS^.lrDeceleration, 
		Jerk:= THIS^.lrJerk);
	IF fbMoveB.Done THEN
		fbMoveB(fbMover:= THIS^.fbMover, Execute:= FALSE);
		THIS^.uiState := 30;
	ELSIF fbMoveB.Error THEN
		METH_Error();
		Prop_ErrorID := 11; //Moverがエラー
	END_IF

	30:
	//0に戻す
	dutTargetPosition.b := 0;
	fbMoveB(
		fbMover:= THIS^.fbMover, 
		Execute:= TRUE, 
		Position:= dutTargetPosition, 
		Velocity:= THIS^.lrVelocity, 
		Acceleration:= THIS^.lrAcceleration, 
		Deceleration:= THIS^.lrDeceleration, 
		Jerk:= THIS^.lrJerk);
	IF fbMoveB.Done THEN
		fbMoveB(fbMover:= THIS^.fbMover, Execute:= FALSE);
		THIS^.uiState := 16#FFFF;
	ELSIF fbMoveB.Error THEN
		METH_Error();
		Prop_ErrorID := 12; //Moverがエラー
	END_IF
	
	16#FFFF:
	METH_Done();
END_CASE]]></ST>
      </Implementation>
    </Method>
    <Method Name="METH_Enter" Id="{11016756-65ab-4b48-9174-b7b207cd2f3d}">
      <Declaration><![CDATA[ {attribute 'hide'}
//{error 'add method implementation or delete method to use base implementation'}
METHOD INTERNAL METH_Enter : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//Input 内部変数に移す
dutPosition := Position;
lrVelocity := Velocity;
lrAcceleration := Acceleration;
lrDeceleration := Deceleration;
lrJerk := Jerk;


]]></ST>
      </Implementation>
    </Method>
    <Method Name="METH_Exit" Id="{b3b1955c-e6f8-4417-a236-61b8c6bdc8fe}">
      <Declaration><![CDATA[ {attribute 'hide'}
//{error 'add method implementation or delete method to use base implementation'}
METHOD INTERNAL METH_Exit : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Prop_Done := FALSE;
Prop_Busy := FALSE;
Prop_CommandAborted := FALSE;
Prop_Error := FALSE;
Prop_ErrorID := 0;

// 内部変数をリフレッシュする
uiState := 0;
lrVelocity := 0;
lrAcceleration := 0;
lrDeceleration := 0;
lrJerk := 0;

//FBの初期化
fbMoveB(fbMover:= THIS^.fbMover, Execute:= FALSE); ]]></ST>
      </Implementation>
    </Method>
    <Method Name="METH_Main" Id="{3dca9f4e-1ab5-4180-8f4a-c59bae17c6b9}">
      <Declaration><![CDATA[ {attribute 'hide'}
//{error 'add method implementation or delete method to use base implementation'}
METHOD INTERNAL METH_Main : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//Doneに遷移
IF SUPER^.bDone AND SUPER^.bExecute THEN
	METH_Done();
	RETURN;
END_IF
//Errorに遷移
IF SUPER^.bError AND SUPER^.bExecute THEN
	METH_Error();
	RETURN;
END_IF
//CommandAbortedに遷移
IF SUPER^.bCommandAborted AND SUPER^.bExecute THEN
	METH_CommandAborted();
	RETURN;
END_IF
//FBをリフレッシュ
//状態がDone, Error, CommandAborted時に Execute FALSE で遷移
IF (SUPER^.bDone OR SUPER^.bError OR SUPER^.bCommandAborted) AND NOT SUPER^.bExecute THEN
	METH_Exit();
	RETURN;
END_IF

//実行開始
IF SUPER^.bExecute AND NOT SUPER^.bBusy THEN
	METH_Execute();
	METH_Enter();
END_IF
//実行中
IF SUPER^.bBusy THEN
	METH_Busy();
END_IF]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_Tilt_B">
      <LineId Id="42" Count="8" />
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_Tilt_B.METH_Busy">
      <LineId Id="5" Count="17" />
      <LineId Id="159" Count="0" />
      <LineId Id="23" Count="14" />
      <LineId Id="56" Count="1" />
      <LineId Id="95" Count="0" />
      <LineId Id="179" Count="0" />
      <LineId Id="96" Count="13" />
      <LineId Id="73" Count="0" />
      <LineId Id="140" Count="0" />
      <LineId Id="142" Count="1" />
      <LineId Id="181" Count="0" />
      <LineId Id="158" Count="0" />
      <LineId Id="145" Count="12" />
      <LineId Id="141" Count="0" />
      <LineId Id="92" Count="2" />
      <LineId Id="4" Count="0" />
    </LineIds>
    <LineIds Name="FB_Tilt_B.METH_Enter">
      <LineId Id="5" Count="5" />
      <LineId Id="15" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="4" Count="0" />
    </LineIds>
    <LineIds Name="FB_Tilt_B.METH_Exit">
      <LineId Id="5" Count="7" />
      <LineId Id="14" Count="2" />
      <LineId Id="4" Count="0" />
      <LineId Id="21" Count="1" />
      <LineId Id="20" Count="0" />
    </LineIds>
    <LineIds Name="FB_Tilt_B.METH_Main">
      <LineId Id="5" Count="29" />
      <LineId Id="4" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>